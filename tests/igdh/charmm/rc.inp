* Optimize hydrogens and waters
*

! Topology files
open  read  unit 1 card name "toppar/top_all27_prot_na.inp"
read  rtf   unit 1 card
close       unit 1

open  read  unit 1 card name "toppar/glycerol.rtf"
read  rtf   unit 1 card appe
close       unit 1

open  read  unit 1 card name "toppar/cys_radical.rtf"
read  rtf   unit 1 card appe
close       unit 1

! Parameter files
open  read  unit 1 card name "toppar/par_all27_prot_na.inp"
read  para  unit 1 card
close       unit 1


!^^^^^^^^^^
open  read       unit 1 card name "A_chain.pdb"
read  sequ pdb   unit 1 
gene  PRTA first none last none setup 
rewi             unit 1
read  coor pdb   unit 1 
close            unit 1

open  read       unit 1 card name "A_gol.pdb"
read  sequ pdb   unit 1 
gene  GOLA setup 
rewi             unit 1
read  coor pdb   unit 1 
close            unit 1

open  read       unit 1 card name "A_wat.pdb"
read  sequ pdb   unit 1 
gene  WATA setup noan nodi
rewi             unit 1
read  coor pdb   unit 1 
close            unit 1


!^^^^^^^^^^
! Apply patches
patch GLUP  PRTA  443   setup
patch ASPP  PRTA  506   setup
patch ASPP  PRTA  558   setup
patch CYSR  PRTA  433   setup


coor    init sele type h* end 
ic      fill pres
ic      para
hbuild 


!^^^^^^^^^^
! Save coordinates before optimization
open   write      unit 1 card name "monomer_pre.pdb"
write  coor pdb   unit 1
close             unit 1

! Fix all but hydrogens and waters
cons fix sele .not. (segid WAT* .or. hydrogen) show end

! Optimize
minimize sd   nstep  50 nprint 20 inbfrq 20
minimize conj nstep 100 nprint 20 inbfrq 20

! Release all constraints 
cons fix sele none end


!^^^^^^^^^^
stream "toppar/radii.str"

open   write         unit 1 card name "monomer.pdb" 
write  coor pdb      unit 1
close                unit 1

open   write         unit 1 card name "monomer.crd"
write  coor card     unit 1
close                unit 1

! xplo cannot be used with crdpsf2pqr 
open   write         unit 1 card name "monomer_xplor.psf"
write  psf card xplo unit 1
close                unit 1

stop
